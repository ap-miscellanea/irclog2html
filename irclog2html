#!/usr/bin/env perl
use 5.010;
use strict;
use utf8;
use Encode;
use Digest::SHA1 qw( sha1_base64 );

use constant CGI_MODE => $0 =~ /\.cgi\z/;

my %ent = qw( < lt > gt & amp ' #39 " #34 );
sub encode_entities { s!([<>&'"])!&$ent{$1};!g for my @c = @_; @c[ 0 .. $#c ] }

sub cgi_exit {
	my ( $status, $body ) = @_;
	print "Status: $status\r\n";
	print "Content-Type: text/plain\r\n";
	print "\r\n$body";
	exit;
}

my @file = ( not CGI_MODE )
	? ( @ARGV ? @ARGV : '-' )
	: $ENV{'IRCLOG'} // $ENV{'REDIRECT_IRCLOG'} // cgi_exit 404, 'Empty.';

# regex by John Gruber
# http://daringfireball.net/2010/07/improved_regex_for_matching_urls
my $url_rx = qr{\b((?:[a-z][\w-]+:(?:/{1,3}|[a-z0-9%])|www\d{0,3}[.|[a-z0-9.\-]+[.[a-z]{2,4}/)(?:[^\s()<>]+|\(([^\s()<>]+|(\([^\s()<>]+\)))*\))+(?:\(([^\s()<>]+|(\([^\s()<>]+\)))*\)|[^\s`!()\[\{\};:'".,<>?«»“”‘’]))}i;

my ( $title ) = @file == 1 ? $file[0] =~ /([^\/]+?)(?:\.(?:irc)?log)?\z/ : '';
my $body = '';
my $num_lines = 0;

for my $fn ( @file ) {
	open my $fh, '<:encoding(UTF-8)', $fn
		or do {
			if ( CGI_MODE ) { cgi_exit +( $!{EACCES} ? 403 : 500 ) x 2 }
			else { die "Couldn't open $fn to read: $!\n" }
		};

	while ( <$fh> ) {
		++$num_lines;
		chomp;
		my $line_id = sha1_base64 encode 'UTF-8', $_;
		my ( $nick, $line ) = split /(?<=>) /, $_, 2;

		$line =~ s{\G(.*?)$url_rx?}{
			encode_entities( $1 )
			. ( $2 ? sprintf '<a href="%s">%1$s</a>', encode_entities( $2 ) : '' )
		}eg;

		$body .=
			sprintf "<table><tr>%s</tr></table>\n",
			join "",
			map { sprintf "<td>%s</td>", $_ }
			sprintf( '<a href="#l%s" id="l%s">%s</a>', $line_id, $line_id, encode_entities $nick ),
			$line;
	}
}

print "Content-Type: text/html; charset=us-ascii\r\n\r\n" if CGI_MODE;

print Encode::encode 'us-ascii', << "END_PAGE", Encode::FB_HTMLCREF;
<html>
<head>
<title>$title@{[ $title ? ' ' : '' ]}($num_lines lines) « irclog2html</title>
<style type="text/css">
tr > td:first-child > a { text-decoration: none; color: inherit }
td { font-family: monospace; font-size: 12px; vertical-align: top; white-space: nowrap }
td + td { padding-left: 0.25em; white-space: normal }
</style>
</head>
<body>
$body
</body>
</html>
END_PAGE
